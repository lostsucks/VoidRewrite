on async chat:

    # Example:
    # [I-1] OWNER lostmatter: message
    # [VII-755] OWNER lostmatter: message

    set {_prestige} to romanized int tag "custom;prestige" of nbt compound of player
    set {_level} to int tag "custom;level" of nbt compound of player
    set {_asyncMsg} to formatted async chat message

    if {_prestige} is not set:
        set {_prestige} to romanized 1
    if {_level} is not set:
        set {_level} to 1

    if {muteList::*} contains player's uuid:
        cancel event
        send action bar with prefix "You are muted!"
        play sound "entity.villager.no" to player
        stop

    if player has permission "chatemojis":
        replace all ":skull:" in {_asyncMsg} with "&7â˜ &r"
        replace all ":heart:" in {_asyncMsg} with "&câ¤&r"
        replace all ":star:" in {_asyncMsg} with "&eâ­&r"
        replace all ":fire:" in {_asyncMsg} with "&6ðŸ”¥&r"
        replace all ":snowflake:" in {_asyncMsg} with "&bâ„&r"
        replace all ":lightning:" in {_asyncMsg} with "&6âš¡&r"
        replace all ":cloud:" in {_asyncMsg} with "&fâ˜&r"
        replace all ":sun:" in {_asyncMsg} with "&eâ˜€&r"
        replace all ":smile:" in {_asyncMsg} with "&aãƒ„&r"
        replace all ":sad:" in {_asyncMsg} with "&9â˜¹&r"

    set {_m::1} to "&5[&d%{_prestige}%-%{_level}%&5] &r%hex fixed player's prefix%%player's name%&7: "
    set {_m::2} to "&f%{_asyncMsg}%"

    set {_merge} to merge components {_m::*}
    set async chat format to {_merge}

# romainzed

expression roman[ize[d]] [numeral] [(of|from)] %integers%:
    get:
        set {_roman::numerals::*} to ("M", "CM", "D", "CD", "C", "XC", "L", "XL", "X", "IX", "V", "IV", "I")
        set {_roman::values::*} to (1000, 900, 500, 400, 100, 90, 50, 40, 10, 9, 5, 4, 1)
        
        loop exprs-1:
            clear {_numeral::*}
            set {_i} to loop-value
            loop (size of {_roman::values::*}) times:
                while {_i} >= {_roman::values::%loop-value-2%}:
                    subtract {_roman::values::%loop-value-2%} from {_i}
                    add {_roman::numerals::%loop-value-2%} to {_numeral::*}
            add (join {_numeral::*} using "") to {_return::*}
        return {_return::*}
